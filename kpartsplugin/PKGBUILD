# Contributor: birdflesh <antkoul at gmail dot com>
# Contributor: Frikilinux <frikilinux at frikilinux.com.ar>
# Current i686 Maintainer: ifaigios <ifaigios_at_gmail_dot_com>

pkgname=kpartsplugin
pkgver=20110823
pkgrel=1
pkgdesc="This plugin uses KDE's KParts to embed file viewers into non-KDE browsers"
arch=('i686' 'x86_64')
url="http://www.unix-ag.uni-kl.de/~fischer/kpartsplugin/"
license=('GPL3' 'BSD')
depends=('kdelibs')
makedepends=('automoc4' 'cmake')
source=("${url}${pkgname}-${pkgver}.tar.bz2" "LICENSE")
md5sums=('bccb80104a6e377cbf839c3f0c0dc762'
         'c4cc811349e40e9f34e77e27b902ad96')

build(){
  sed -i 's/nsbrowser/mozilla/g' ${pkgname}-${pkgver}/CMakeLists.txt
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
}