# Current i686 Maintainer: ifaigios <ifaigios_at_gmail_dot_com>
# Contributor: halim <sagikliwon@gmail.com>

pkgname=wxbase
pkgver=2.8.12
pkgrel=1
pkgdesc="wxBase - wxWidgets base libraries for no X install"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('zlib')
conflicts=('wxgtk')
source=("http://prdownloads.sourceforge.net/wxwindows/wxGTK-${pkgver}.tar.gz")
md5sums=('b12c78d947d89e1120443761f1b7693b')

build() {
  cd ${srcdir}/wxGTK-${pkgver}

  ./configure --prefix=/usr --libdir=/usr/lib \
              --disable-gui --disable-debug --enable-unicode --disable-compat24 \
              --disable-compat26 --with-zlib=sys --without-odbc \
              --without-expat --without-libmspack --without-subdirs

  make CC="gcc"
}

package() {
  cd ${srcdir}/wxGTK-${pkgver}
  make DESTDIR=${pkgdir} install

  cd docs || return 1
  install -D -m644 licence.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
