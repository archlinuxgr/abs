# Contributor: birdflesh <antkoul at gmail dot com>
# Current i686 Maintainer: ifaigios <ifaigios_at_gmail_dot_com>

pkgname=hunspell-el
pkgver=0.8
pkgrel=1
pkgdesc="Greek dictionary for hunspell"
arch=('any')
url="http://elspell.math.upatras.gr/"
license=('MPL' 'GPL' 'LGPL')
optdepends=('hunspell:	the spell checking libraries and apps')
source=("${url}files/ooffice/el_GR-${pkgver}.zip")
md5sums=('83c61e548d8c1feea648dbb11731ea74')

package() {
	install -dm755 ${pkgdir}/usr/share/hunspell
	install -m644 el_GR.aff ${pkgdir}/usr/share/hunspell
	install -m644 el_GR.dic ${pkgdir}/usr/share/hunspell

	# symlinks
	install -dm755 ${pkgdir}/usr/share/myspell/dicts
	pushd $pkgdir/usr/share/myspell/dicts
	for file in $pkgdir/usr/share/hunspell/*; do
		ln -sv /usr/share/hunspell/$(basename $file) .
	done
	popd

	# docs
	install -dm755 ${pkgdir}/usr/share/doc/$pkgname
	install -m644 README_el_GR.txt $pkgdir/usr/share/doc/$pkgname
}

