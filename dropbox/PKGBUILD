# Contributor: Massimiliano Torromeo <Massimiliano DOT Torromeo AT gmail DOT com>
# Contributor: Tom < tomgparchaur at gmail dot com >
# Adslgr32 Maintainer: Axilleas P <markelos archlinux gr>

pkgname=dropbox
pkgver=1.1.35
pkgrel=1
pkgdesc="Secure backup, sync and sharing made easy."
arch=("i686" "x86_64")
url="http://www.dropbox.com"
license=(custom)
depends=("bzip2" "dbus-glib" "gtk2" "libsm")
conflicts=("dropbox-experimental")
options=('!strip')

_source_arch="x86"
[ "$CARCH" = "x86_64" ] && _source_arch="x86_64"

md5sums=('26f65450fd3e7d95e796a871dd7a53df' '9ec50da2ce59ed8c17606394b9c5e1c0' 'b34263a03e5ebfb3ec6615b35273a78f' '619b5a2e49b48f564423a850c94312d7')
[ "$CARCH" = "x86_64" ] && md5sums[0]='a3956852eba85120c741fe914c01d3ad'

source=("https://dl-web.dropbox.com/u/17/${pkgname}-lnx.${_source_arch}-${pkgver}.tar.gz" "dropbox.png" "dropbox.desktop" "terms.html")

build() {
	install -d "$pkgdir/opt"
	rm "$srcdir/.dropbox-dist/libstdc++.so.6"
	cp -R "$srcdir/.dropbox-dist" "$pkgdir/opt/dropbox"

	find "$pkgdir/opt/dropbox/" -type f -exec chmod 644 {} \;
	find "$pkgdir/opt/dropbox/" -type d -exec chmod 755 {} \;
	chmod 755 "$pkgdir/opt/dropbox/dropboxd"
	chmod 755 "$pkgdir/opt/dropbox/dropbox"
	chown -R root.root "$pkgdir/opt/dropbox"

	install -d "$pkgdir/usr/bin"
	ln -s "/opt/dropbox/dropboxd" "$pkgdir/usr/bin/dropboxd"

	install -D -m 644 "$srcdir/dropbox.desktop" "$pkgdir/usr/share/applications/dropbox.desktop"
	install -D -m 644 "$srcdir/dropbox.png" "$pkgdir/usr/share/pixmaps/dropbox.png"
	install -D -m 644 "$srcdir/terms.html" "$pkgdir/usr/share/licenses/$pkgname/terms.html"
}
